plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'maven-publish'
}

group = 'com.github.Tap-Payments'

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--always'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (ignored) {
        return "1.0.0"
    }
}

android {
    namespace 'company.tap.tapbenefitpay'
    compileSdkVersion 34

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 34
        versionCode 1
        versionName getVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'

    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }
}

dependencies {
    def lifecycle_version = "2.6.2"

    api "org.jetbrains.kotlin:kotlin-stdlib:1.9.0"
    api 'androidx.core:core-ktx:1.13.1'
    api 'androidx.appcompat:appcompat:1.6.1'
    api 'com.google.android.material:material:1.12.0'
    api 'androidx.constraintlayout:constraintlayout:2.1.4'
    api 'androidx.webkit:webkit:1.11.0'

    api 'com.google.code.gson:gson:2.10'
    api 'com.squareup.retrofit2:retrofit:2.9.0'
    api 'com.squareup.okhttp3:logging-interceptor:4.10.0'
    api "com.squareup.retrofit2:converter-gson:2.9.0"

    api "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    api "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    api "androidx.lifecycle:lifecycle-process:$lifecycle_version"

    api 'com.intuit.sdp:sdp-android:1.1.0'

    // Tap dependencies
    api 'com.github.Tap-Payments:TapNetwrok-Android:0.2.2.44'
    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.5.1")
}



// Javadoc for Android (variant-aware)
task releaseJavadoc(type: Jar) {
    archiveClassifier.set('javadoc')
    from android.sourceSets.main.java.srcDirs
    // optional: include kotlin sources if you have Kotlin
    from android.sourceSets.main.kotlin.srcDirs
}
afterEvaluate {
    android.libraryVariants.all { variant ->
        // Use the provider API instead of mergeResources directly
        variant.mergeResourcesProvider.configure { mergeTask ->
            doLast {
                def resDir = mergeTask.outputDir.get().asFile
                def valuesDir = new File(resDir, "values")

                def dimensFile = new File(valuesDir, "dimens_sdp.xml")
                if (dimensFile.exists()) {
                    def updated = dimensFile.getText("UTF-8")
                            .replaceAll('name="_', 'name="tap_') // rename _45sdp â†’ tap_45sdp
                    dimensFile.write(updated, "UTF-8")
                }
            }
        }
    }
    publishing {
        publications {
            create("release", MavenPublication) {
                // Publish AAR
                artifact("$buildDir/outputs/aar/${project.name}-release.aar") {
                    builtBy tasks.named("assembleRelease")
                }

                groupId = "com.github.Tap-Payments"
                artifactId = "BenefitPay-Android"
                version = android.defaultConfig.versionName ?: "1.0.3.28"

                // Attach sources and javadoc
                artifact(tasks.named("releaseSourcesJar"))
                artifact(tasks.named("releaseJavadoc"))
            }
        }
    }

    // Ensure metadata generation waits for sources/javadoc
    tasks.named("generateMetadataFileForReleasePublication") {
        dependsOn(tasks.named("releaseSourcesJar"))
        dependsOn(tasks.named("releaseJavadoc"))
    }
}